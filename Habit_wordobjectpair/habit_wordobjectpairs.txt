# This protocol is based on Experiment 3 of the paper cited below
# Werker, J. F., Cohen, L. B., Lloyd, V. L., Casasola, M., & Stager, C. L. (1998). Acquisition of wordâ€“object associations by 14-month-old infants. Developmental psychology, 34(6), 1289.

SIDES ARE {CENTER, RIGHT, LEFT}
DISPLAYS ARE {CENTER}
LIGHTS ARE {LEFT, CENTER, RIGHT}

LET deeb_blue = "C:\Users\ldev\Desktop\ExampleProtocols\HabitExample\stim\deeb_blue.wmv"
LET deeb_green = "C:\Users\ldev\Desktop\ExampleProtocols\HabitExample\stim\deeb_green.wmv"
LET geff_blue = "C:\Users\ldev\Desktop\ExampleProtocols\HabitExample\stim\geff_blue.wmv"
LET geff_green = "C:\Users\ldev\Desktop\ExampleProtocols\HabitExample\stim\geff_green.wmv"
LET mip_round = "C:\Users\ldev\Desktop\ExampleProtocols\HabitExample\stim\mip_round.mp4"

LET habit_pairs = {deeb_blue, deeb_green, geff_blue, geff_green}
LET test_trials = {deeb_blue, deeb_green, geff_blue, geff_green}

DEFINE WINDOWSIZE 3
DEFINE WINDOWOVERLAP NO
DEFINE CRITERIONREDUCTION 0.65
DEFINE BASISCHOSEN FIRST
DEFINE WINDOWTYPE FIXED

STEP 1
Phase Pretrial Start

STEP 2
LIGHT CENTER BLINK 250
UNTIL KEY C

STEP 3
LIGHT CENTER OFF
Trial Start

STEP 4
VIDEO CENTER mip_round LOOP
UNTIL SINGLELOOKAWAY mip_round GREATERTHAN 1000
UNTIL TIME 14000

STEP 5
Trial End
VIDEO CENTER OFF

STEP 6
Phase End

STEP 7
Phase Habituation Start
LET pair = (FROM habit_pairs RANDOM)

STEP 8
LIGHT CENTER BLINK 250
UNTIL KEY C

STEP 9
LIGHT CENTER OFF
Trial Start

STEP 10
VIDEO CENTER pair LOOP
UNTIL SINGLELOOKAWAY pair GREATERTHAN 1000
UNTIL TIME 20000

STEP 11
Trial End
VIDEO CENTER OFF

STEP 12
LOOP STEP 8
UNTIL 19 TIMES
UNTIL CRITERIONMET

STEP 13
Phase End

STEP 14
Phase Test Start

STEP 15
LIGHT CENTER BLINK 250
UNTIL KEY C

STEP 16
LIGHT CENTER OFF
Trial Start

STEP 17
LET test_item = (TAKE test_trials RANDOM)
VIDEO CENTER test_item LOOP
UNTIL SINGLELOOKAWAY test_item GREATERTHAN 1000
UNTIL TIME 14000

STEP 18
Trial End
VIDEO CENTER OFF

STEP 19
LOOP STEP 15
UNTIL 3 TIMES

STEP 20
Phase End

STEP 21
Phase Posttest Start

STEP 22
LIGHT CENTER BLINK 250
UNTIL KEY C

STEP 23
LIGHT CENTER OFF
Trial Start

STEP 24
VIDEO CENTER mip_round LOOP
UNTIL SINGLELOOKAWAY mip_round GREATERTHAN 1000
UNTIL TIME 14000

STEP 25
Trial End
VIDEO CENTER OFF

STEP 26
Phase End